# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Link Dupont <link@subpop.net>

_pkgname=dbus
pkgname=$_pkgname-static
pkgver=1.12.20
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="https://wiki.freedesktop.org/www/Software/dbus/"
arch=(x86_64)
license=(GPL custom)
depends=(expat-static)
makedepends=(xmlto python yelp-tools git autoconf-archive)
options=(!emptydirs staticlibs)
#source=("git+https://gitlab.freedesktop.org/dbus/dbus.git?signed#tag=dbus-$pkgver"
source=("git+https://gitlab.freedesktop.org/dbus/dbus.git#tag=dbus-$pkgver"
        no-fatal-warnings.diff)
sha256sums=('SKIP'
            '6958eeec07557b92a28419eb1702331ee2f0a6fd17285e37dfb6130b9fa4cf6e')
#validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90') # Simon McVittie <simon.mcvittie@collabora.co.uk>

prepare() {
  cd dbus

  # Allow us to enable checks without them being fatal
  patch -Np1 -i ../no-fatal-warnings.diff

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd dbus
  ./configure \
    --prefix=/usr/static \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/static/lib/dbus-1.0 \
    runstatedir=/run \
    --enable-inotify \
    --disable-audit \
    --disable-systemd \
    --enable-user-session \
    --disable-xml-docs \
    --disable-doxygen-docs \
    --disable-ducktype-docs \
    --disable-shared \
    --enable-static \
    --without-x
  make
}

check() {
  make -C dbus check
}

package() {
  DESTDIR="$pkgdir" make -C dbus install
  rm -r "$pkgdir"/{etc,var,usr/lib,usr/static/bin,usr/static/share/doc}

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 dbus/COPYING
}

# vim:set sw=2 et:
