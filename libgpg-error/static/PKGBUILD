# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

_pkgname=libgpg-error
pkgname=$_pkgname-static
pkgver=1.41
pkgrel=1
pkgdesc="Support library for libgcrypt"
arch=(x86_64)
url="https://www.gnupg.org"
license=('LGPL')
depends=('glibc')
options=(staticlibs)
source=(https://www.gnupg.org/ftp/gcrypt/libgpg-error/${_pkgname}-${pkgver}.tar.bz2{,.sig})
# https://www.gnupg.org/download/integrity_check.html
sha1sums=('66d6270511a48bac0bf347330e7a12c62f3a1ab4'
          'SKIP')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'  # Werner Koch
              '031EC2536E580D8EA286A9F22071B08A33BD3F06'  # NIIBE Yutaka (GnuPG Release Key) <gniibe@fsij.org>
              '6DAA6E64A76D2840571B4902528897B826403ADA') # "Werner Koch (dist signing 2020)"

prepare() {
  cd ${_pkgname}-${pkgver}
  autoreconf -vfi
}

build() {
  cd ${_pkgname}-${pkgver}
  ./configure --prefix=/usr/static --disable-shared --enable-static
  make
}

check() {
  cd ${_pkgname}-${pkgver}
  make check
}

package() {
  cd ${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install
  rm -r "$pkgdir"/usr/static/share/{man,info}
}
