# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_pkgname=libxau
pkgname=$_pkgname-static
pkgver=1.0.9
pkgrel=3
pkgdesc="X11 authorisation library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('xorgproto')
license=('custom')
options=(!emptydirs staticlibs)
source=(${url}/releases/individual/lib/libXau-${pkgver}.tar.bz2{,.sig})
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"
sha512sums=('3ca454ba466a807ea28b0f715066d73dc76ad312697b121d43e4d5766215052e9b7ffb8fe3ed3e496fa3f2a13f164ac692ff85cc428e26731b679f0f06a1d562'
            'SKIP')

build() {
  cd libXau-${pkgver}
  ./configure --prefix=/usr/static --disable-shared --enable-static --sysconfdir=/etc
  make
}

package() {
  cd libXau-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -r "$pkgdir"/usr/static/share/man

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
