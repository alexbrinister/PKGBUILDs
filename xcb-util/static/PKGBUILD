# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=xcb-util
pkgname=$_pkgname-static
pkgver=0.4.0
pkgrel=3
pkgdesc="Utility libraries for XC Binding"
arch=('x86_64')
url="https://xcb.freedesktop.org"
license=('custom')
depends=('libxcb-static>=1.7')
makedepends=('gperf' 'xorg-util-macros' 'xorgproto')
options=(staticlibs)
source=("https://xcb.freedesktop.org/dist/${_pkgname}-${pkgver}.tar.bz2")
sha512sums=('e60aaa6f582eacd05896c5fd7c8417938318a1288146f3a5b339f77eed24e211c6099963f8813daa621c94173d2934228936b491c0ed79b09a8a67d835867d0e')

build() {
  cd ${_pkgname}-${pkgver}
  export PKG_CONFIG_PATH=/usr/static/lib/pkgconfig:/usr/lib/pkgconfig
  ./configure --prefix=/usr/static --disable-shared --enable-static
  make
}

check() {
  cd ${_pkgname}-${pkgver}
  make check
}

package() { 
  cd ${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -D -m644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
