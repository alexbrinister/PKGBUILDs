# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

_pkgname=xz
pkgname=$_pkgname-static
pkgver=5.2.5
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('x86_64')
url='https://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('glibc')
options=(!emptydirs staticlibs)
source=("https://tukaani.org/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('f6f4910fd033078738bd82bfba4f49219d03b17eb0794eb91efbae419f4aba10'
            'SKIP')
validpgpkeys=('3690C240CE51B4670D30AD1C38EE757D69184620')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	./configure --prefix=/usr/static \
		--disable-rpath \
		--disable-shared \
		--enable-static
	make
}

check() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}/"
	ln -sf /usr/static/share/doc/xz/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
	rm -r "$pkgdir"/usr/static/bin
	rm -r "$pkgdir"/usr/static/share/{man,doc,locale}
}
